buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

plugins {
    id 'maven-publish'
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'groovy'

sourceCompatibility = 1.7
group = 'com.xinthink.gradle'
apply from: "$rootDir/version.gradle"

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.ajoberstar.grgit:grgit-gradle:3.1.1'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// run `publishPluginPublicationToMavenLocal` to install to local maven repo
publishing {
    publications {
        plugin(MavenPublication) {
            groupId = group

            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = BINTRAY_USER
    key = BINTRAY_KEY
    publications = ["plugin"]

    dryRun = false
    publish = true

    pkg {
        repo = "maven"
        name = project.name
        setLicenses("Apache-2.0")
        vcsUrl = "https://github.com/xinthink/gradle-git-repo-plugin"
        version {
            name = project.version
            vcsTag = "v${project.version}"
        }
    }
}
